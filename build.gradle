plugins {
  id "java-library"
  id "org.sonarqube" version "3.3"
}

wrapper {
  gradleVersion = "7.4"
}

dependencies {
  implementation project(":app")
  implementation project(":core")
  implementation project(":persistence")
  implementation project(":rest")
}

jar {
  manifest {
    attributes("Main-Class": "com.javintx.crm.Application")
  }

  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }

  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

allprojects {
  group "com.javintx.crm"
  version "1.0.0"

  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: "java"
  apply plugin: "jacoco"

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(17)
    }
  }

  check.finalizedBy(jacocoTestReport)

  jacocoTestReport {
    reports {
      xml.required = true
      html.required = true
    }
  }

  dependencies {
    testImplementation "org.assertj:assertj-core:3.22.0"
    testImplementation "org.mockito:mockito-junit-jupiter:4.3.1"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
  }

  test {
    useJUnitPlatform()
  }
}

sonarqube {
  properties {
    property "sonar.projectKey", "javintx_the-crm-service"
    property "sonar.organization", "javintx"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}
